#include "ui/menudef.h"
#include "ui/menumacros.h"

// Defines //
#define WINDOW_X		0
#define WINDOW_Y		0
#define WINDOW_WIDTH	640
#define WINDOW_HEIGHT	480
#define GROUP_NAME		"grpProfileCreateInitial"
		
// Create Profile Menu //
#define INITIAL_PROFILEDEFAULT_SETTINGS		uiScript systemCvarsGet ;				\
											uiScript profileCvarsGet ;				\
											setcvar ui_glCustom 1 ;					\
											setcvar ui_r_gamma 1.3 ;				\
											setcvar ui_sensitivity 5 ;				\
											setcvar ui_profile_mousePitch 0 ;		\
											setcvar ui_handedness 0 ;				\
											setcvar ui_rate 0 ;						\
											uiScript update ui_glCustom				\

// Opener //
menuDef {
	name		"profile_create_initial_opener"
	visible		0
	fullscreen	0
	rect		WINDOW_X WINDOW_Y WINDOW_WIDTH WINDOW_HEIGHT
	
	onOpen {
		INITIAL_PROFILEDEFAULT_SETTINGS ;

		open profile_create_initial ;
		close profile_create_initial_opener	;
		
		conditionalscript com_recommendedSet 0
			( "clearfocus" )
			( "abort" )

		conditionalscript com_recommended 3
			( "setcvar ui_glCustom 1 ; uiScript update ui_glCustom" )
			( "copycvar com_recommended ui_glCustom ; uiScript update ui_glCustom" ) "-1"
	}
}

menuDef {
	name		"profile_create_initial"
	visible		0
	fullscreen	0
	rect		WINDOW_X WINDOW_Y WINDOW_WIDTH WINDOW_HEIGHT
	style		WINDOW_STYLE_FILLED
	popup
	
	fadeClamp	0.5
	
	onOpen {
		setEditFocus "efleftProfile Name:" ;
	
		conditionalscript ui_profile_create_error 1 
			( "clearcvar ui_profile_create_error ; abort" )
			( "clearfocus" )

		setitemcolor background backcolor 0 0 0 0 ;
		fadein background ;
		setEditFocus "efleftProfile Name:" ;

		/*conditionalscript com_recommendedSet 0
			( "clearfocus" )
			( "abort" )

		conditionalscript com_recommended 3
			( "setcvar ui_glCustom 1 ; uiScript update ui_glCustom" )
			( "copycvar com_recommended ui_glCustom ; uiScript update ui_glCustom" ) "-1"*/
	}
	
	
// Background //


// Subwindows //
#define SUBWINDOW_WIDTH		350
#define SUBWINDOW_HEIGHT	390
#define SUBWINDOW_X			.5*(WINDOW_WIDTH-SUBWINDOW_WIDTH)
#define SUBWINDOW_Y			.5*(WINDOW_HEIGHT-SUBWINDOW_HEIGHT)

	SUBWINDOWBLACK( SUBWINDOW_X, SUBWINDOW_Y, SUBWINDOW_WIDTH, SUBWINDOW_HEIGHT, "CREA PROFILO" )

	LABEL( SUBWINDOW_X+4, SUBWINDOW_Y+16, (SUBWINDOW_WIDTH)-8, 10, "Scegli per favore le tue impostazioni iniziali qui sotto. Potrai cambiarle in ogni momento e avere accesso ad opzioni piu' avanzate visitando il Menu' Opzioni. Se piu' di una persona gioca a W:ET con questo utente, puoi creare profili aggiuntivi dal Menu' Profili.", .2, ITEM_ALIGN_LEFT, 0, 8 )

	itemDef {
		name		"efleftbackPlayer Alias:"
		group		GROUP_NAME
		rect		$evalfloat((SUBWINDOW_X+4+32)+80+6) $evalfloat(SUBWINDOW_Y+88) $evalfloat((SUBWINDOW_WIDTH)-8-64-80-6) $evalfloat(10)
		style		WINDOW_STYLE_FILLED
		backcolor	.5 .5 .5 .2
		visible		1
		decoration
	}
	EDITFIELDLEFT( SUBWINDOW_X+4+32, SUBWINDOW_Y+88, (SUBWINDOW_WIDTH)-8-64, 10, "Alias Giocatore:", .2, 8, "ui_profile", 36, 23, "Inserisci qui l'alias che vuoi far vedere agli altri sui server" )
	
	MULTIACTION( SUBWINDOW_X+4, SUBWINDOW_Y+112, (SUBWINDOW_WIDTH)-8, 10, "Lingua:", .2, 8, "cl_language", cvarFloatList { "Inglese (predef.)" 0 "Francese" 1 "Tedesco" 2 "Italiano" 3 "Spagnolo" 4 "Polacco" 5 "Olandese" 6 "Ceco" 7 "Svedese" 8 "Finlandese" 9 "Danese" 10}, uiScript update "cl_language", "Scegli la lingua desiderata" )
	MULTIACTION( SUBWINDOW_X+4, SUBWINDOW_Y+136, (SUBWINDOW_WIDTH)-8, 10, "Qualita' Video:", .2, 8, "ui_glCustom", cvarFloatList { "Razzo" 3 "Veloce" 2 "Normale" 1 "Alta" 0 }, uiScript update "ui_glCustom", "Scegli le impostazioni video iniziali, potranno essere cambiate successivamente dalle opzioni" )
	MULTIACTION( SUBWINDOW_X+4, SUBWINDOW_Y+150, (SUBWINDOW_WIDTH)-8, 10, "Risoluzione:", .2, 8, "ui_r_mode", cvarFloatList { "risoluzione desktop" -2 "320*240" 0 "400*300" 1 "512*384" 2 "640*480" 3 "800*600" 4 "960*720" 5 "1024*768" 6 "1152*864" 7 "1280*1024" 8 "1600*1200" 9 "2048*1536" 10 "856*480 Wide Screen" 11 "1366x768 (16:9)" 12 "1440x900 (16:10)" 13 "1680x1050 (16:10)" 14 "1600x1200" 15 "1920x1080 (16:9)" 16 "1920x1200 (16:10)" 17 "2560x1600 (16:10)" 18 }, uiScript glCustom, "Scegli la tua risoluzione video preferita - se non sei sicuro di cosa voglia dire, lascia quella predefinita" )
	SLIDER( SUBWINDOW_X+4, SUBWINDOW_Y+174, (SUBWINDOW_WIDTH)-8, 10, "Luminosita':", .2, 8, "ui_r_gamma" 1 0.5 2, "Imposta la luminosita' del gioco" )
	CVARFLOATLABEL( SUBWINDOW_X+4, SUBWINDOW_Y+174, (SUBWINDOW_WIDTH)-8, 10, "ui_r_gamma", .2, ITEM_ALIGN_RIGHT, $evalfloat((SUBWINDOW_WIDTH)-8), 8 )

	MULTIACTION( SUBWINDOW_X+4, SUBWINDOW_Y+198, (SUBWINDOW_WIDTH)-8, 10, "Connessione:", .2, 8, "ui_rate", cvarFloatList { "Modem" 4000 "ISDN" 5000 "LAN/CABLE/xDSL" 25000 } cvarListUndefined "Unselected", uiScript update "ui_setRate", "Imposta il tuo tipo di connessione, se non sei sicuro seleziona Modem" )

	SLIDER( SUBWINDOW_X+4, SUBWINDOW_Y+222, (SUBWINDOW_WIDTH)-8, 10, "Sensibilita' Mouse:", .2, 8, "ui_sensitivity" 5 1 30, "Usa questo cursore per impostare la sensibilita' del mouse" )
	CVARFLOATLABEL( SUBWINDOW_X+4, SUBWINDOW_Y+222, (SUBWINDOW_WIDTH)-8, 10, "ui_sensitivity", .2, ITEM_ALIGN_RIGHT, $evalfloat((SUBWINDOW_WIDTH)-8), 8 )
	YESNO( SUBWINDOW_X+4, SUBWINDOW_Y+246, (SUBWINDOW_WIDTH)-8, 10, "Inverti Mouse:", .2, 8, "ui_profile_mousePitch", "Seleziona questo se preferisci dei controlli tipo simulatore di volo" )

	MULTI( SUBWINDOW_X+4, SUBWINDOW_Y+270, (SUBWINDOW_WIDTH)-8, 10, "Orientamento:", .2, 8, "ui_handedness", cvarFloatList { "Right" 0 "Left" 1 }, "Destro o Mancino? Questo mette le impostazioni di base\nche puoi modificare dopo dal menu' opzioni" )
	
	BUTTON( (SUBWINDOW_X+4)+.5*((SUBWINDOW_WIDTH)-164), SUBWINDOW_Y+284, 164, 14, "IMPOSTAZIONI PREDEFINITE", .24, 11, DEFAULT_SETTINGS ;
		conditionalscript com_recommendedSet 0
		( "clearfocus" )
		( "abort" )
		
		conditionalscript com_recommended 3
		( "setcvar ui_glCustom 1 ; uiScript update ui_glCustom" )
		( "copycvar com_recommended ui_glCustom ; uiScript update ui_glCustom" ) "-1" )
	
	LABEL( SUBWINDOW_X+4, SUBWINDOW_Y+314, (SUBWINDOW_WIDTH)-8-4, 10, "^3Grazie per aver installato ET:Legacy.\n\n^3Visita ^1www.etlegacy.com^3 for ulteriori informazioni.\n\n^3Teniamo vivo ET!", .2, ITEM_ALIGN_LEFT, 0, 8 )

	BUTTON( SUBWINDOW_X+6, SUBWINDOW_Y+SUBWINDOW_HEIGHT-24, .5*(SUBWINDOW_WIDTH-18), 18, "ESCI", .3, 14, close profile_create_initial ; close main ; open credits_quit )
	BUTTON( SUBWINDOW_X+6+.5*(SUBWINDOW_WIDTH-18)+6, SUBWINDOW_Y+SUBWINDOW_HEIGHT-24, .5*(SUBWINDOW_WIDTH-18), 18, "CREA", .3, 14, conditionalscript UIProfileValidName 2
		( "clearFocus" )
		( "setcvar ui_profile_create_error \\\"Non posso creare il profilo. Metti per favore un alias valido.\\\" ; open profile_create_error ; abort" )
		conditionalscript ui_rate 0
		( "clearFocus" )
		( "setcvar ui_profile_create_error \\\"Non posso creare il profilo. Seleziona per favore il tipo di connessione. Se non sei sicuro su cosa mettere, scegli Modem.\\\" ; open profile_create_error ; abort" )
		conditionalscript UIProfileAlreadyExists 2
		( "setcvar ui_profile_create_error \\\"Non posso creare il profilo. Esiste gia' un profilo con questo nome.\\\" ; open profile_create_error ; abort" )
		( "clearFocus" )
																																		
		close profile_create_initial ;
		uiscript createProfile ;
		uiScript vidSave ;
		uiscript setDefaultProfile ;
		uiscript applyProfile ;
		execwolfconfig 1 ;
		uiscript systemCvarsApply ;
		uiscript profileCvarsApply ;
		setcvar ui_showtooltips 1 ;
		uiscript loadCachedServers ;
		exec vid_restart ; )
}
